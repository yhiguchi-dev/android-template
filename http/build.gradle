plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'org.jetbrains.dokka'
    id 'com.diffplug.spotless'
}

android {
    compileSdk 32

    defaultConfig {
        minSdk 24
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        kotlinOptions {
            freeCompilerArgs += [
                '-opt-in=kotlin.RequiresOptIn'
            ]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
    lint {
        checkDependencies true
    }
    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            if (variant.name == android.buildTypes.release.name) {
                output.outputFileName = "${archivesBaseName}.aar"
            }
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3'

    testImplementation 'org.jetbrains.kotlin:kotlin-reflect:1.7.10'
    testImplementation 'ch.qos.logback:logback-classic:1.2.11'
    testImplementation 'io.kotest:kotest-runner-junit5:5.4.1'
    testImplementation 'io.kotest:kotest-framework-concurrency:5.3.2'
    testImplementation 'io.kotest.extensions:kotest-extensions-wiremock:1.0.3'
    testImplementation 'io.mockk:mockk:1.12.5'
    testImplementation 'io.mockk:mockk-agent-jvm:1.12.5'
}

spotless {
    kotlin {
        target 'src/**/*.kt'
        ktlint().editorConfigOverride(["indent_size": 2])
    }
}

dokkaHtml.configure {
    dokkaSourceSets {
        named('main') {
            noAndroidSdkLink.set(false)
        }
    }
}
