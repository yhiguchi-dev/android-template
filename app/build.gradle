plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.dokka'
    id 'com.diffplug.spotless'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "dev.yhiguchi.template"
        minSdk 24
        targetSdk 31
        versionCode getVersionCode(applicationVersion)
        versionName applicationVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-debug-" + buildDate
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
    lint {
        checkDependencies true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation 'androidx.activity:activity-compose:1.4.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    testImplementation 'io.kotest:kotest-runner-junit5:5.1.0'
    testImplementation 'io.kotest:kotest-framework-concurrency:5.1.0'
    testImplementation 'io.kotest.extensions:kotest-extensions-wiremock:1.0.3'
    testImplementation 'io.mockk:mockk:1.12.3'
    testImplementation 'io.mockk:mockk-agent-jvm:1.12.3'

}

spotless {
    kotlin {
        target 'src/**/*.kt'
        ktlint().userData([indent_size:'2'])
    }
}

dokkaHtml.configure {
    dokkaSourceSets {
        named('main') {
            noAndroidSdkLink.set(false)
        }
    }
}

static def getVersionCode(String version) {
    def versions = version.split('\\.')
    def major = versions[0].toInteger() * 1000 ** 2
    def minor = versions[1].toInteger() * 1000
    def patch = versions[2].toInteger()
    return major + minor + patch
}
